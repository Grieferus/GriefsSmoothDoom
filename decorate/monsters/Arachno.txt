ACTOR SDArachnotron : Arachnotron replaces Arachnotron
{
	GibHealth 50
	States
	{
	Spawn:
		ARAC A 5 A_Look
		ARID B 5 A_Look
		Loop
	See:
		ARAC A 20
		ARAC A 3 A_BabyMetal
		ARAC BCDEF 3 A_Chase
		ARAC G 3 A_BabyMetal
		ARAC HIJKL 3 A_Chase
		Goto See+1
	Missile:
		ARAC A 20 A_FaceTarget
		ARAT C 1
		ARAT A 1 Bright
		ARAT B 2 Bright A_BspiAttack
		ARAT A 1 Bright
		ARAT C 3
		ARAT C 1 A_SpidRefire
		Goto Missile+1
	Pain:
		ARPA A 3
		ARPA B 3 A_Pain
		Goto See+1
	Crush:
		PL16 A -1 A_PlaySound("gib/squish")
		Stop
	Death.GibbyHaynes:
		TNT1 A 0 A_JumpIf(GetCVAR("SD_SSGGore")==0, "Death")
		TNT1 A 0 A_JumpIfCloser(128, "XDeath")
		Goto Death
	Death.Saw:
		TNT1 A 0 A_JumpIf(GetCVAR("SD_SawGore")==0, "Death")
		Goto XDeath
	Death:
		SARZ A 3
		SARZ B 3 A_Scream
		SARZ C 3 A_NoBlocking
		SARZ DEFGHIJKL 3
		SARZ M -1 A_BossDeath
		Stop
	XDeath:
		ARXD A 3
		ARXD B 3 A_Scream
		ARXD CD 2 A_NoBlocking
		TNT1 A 0 A_JumpIf(GetCVAR("SD_Gore")==0, +2)
		TNT1 A 0 A_SpawnItemEx("SmallGoreController")
		ARXD EFGHI 3
		ARXD JK 4
		ARXD L -1 A_Bossdeath
		Stop
	Death.Massacre:
		ARXD A 3
		ARXD B 3 A_Scream
		ARXD CD 2 A_NoBlocking
		ARXD EFGHI 3
		ARXD JK 4
		ARXD L -1 A_Bossdeath
		Stop
	Raise:
		SARZ LKJIHGFEDCBA 3
		Goto See
	}
}

ACTOR SDArachnotronPlasma : ArachnotronPlasma replaces ArachnotronPlasma
{
	decal plasmascorchlower
	States
	{
	Spawn:
		TNT1 A 0 A_JumpIf(GetCVAR("SD_FancyPuff")==1, "FancySpawn")
		BAPL ABCD 2 Bright
		Loop
	FancySpawn:
		BAPL ABCD 2 Bright A_SpawnItemEx("GreenSparkle", random(-20, 20), random(-20, 20), random(0, 20))
		Loop
	Death:
		TNT1 A 0 A_JumpIf(GetCVAR("SD_FancyPuff")==1, "FancyDeath")
		GPLD ABCDEFGHIJKL 2 Bright
		Stop
	FancyDeath:
		TNT1 AA 0 A_SpawnItemEx("GreenSparkle2", 0, 0, 0, random(1,3),random(-1,1),random(0,-2))
		TNT1 AA 0 A_SpawnItemEx("GreenSparkle2", 0, 0, 0, random(1,2),random(-3,3),random(-5,3))
		GPLD ABCDEFGHIJKL 2 Bright
		Stop
	}
}