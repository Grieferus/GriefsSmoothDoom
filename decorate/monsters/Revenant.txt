ACTOR SDRevenant : Revenant Replaces Revenant
{
	Gibhealth 60
	States
	{
	Spawn:
		REVI AB 10 A_Look
		Loop
	See:
		RSKE ABCDEFGH 3 A_Chase
		Loop
	Melee:
		SSKE A 0 A_FaceTarget
		SSKE A 3 A_SkelWhoosh
		SSKE B 3
		SSKE C 3 A_FaceTarget
		SSKE D 3
		SSKE E 3 A_SkelFist
		SSKE D 3
		Goto See
	Missile:
		RMIS AB 3 A_FaceTarget
		RMIS C 3 bright A_FaceTarget
		RMIS D 3 bright A_SkelMissile
		RMIS EC 3 bright A_FaceTarget
		RMIS FG 3 A_FaceTarget
		RMIS H 6 A_FaceTarget
		Goto See
	Pain:
		REVP A 5
		REVP B 5 A_Pain
		Goto See
	Death.GibbyHaynes:
		TNT1 A 0 A_JumpIf(GetCVAR("SD_SSGGore")==0, "Death")
		TNT1 A 0 A_JumpIfCloser(128, "XDeath")
		Goto Death
	Death.Saw:
		TNT1 A 0 A_JumpIf(GetCVAR("SD_SawGore")==0, "Death")
		Goto XDeath
	Death:
		TNT1 A 0 A_JumpIf(GetCVAR("SD_MoreDeaths")==0, "Death1")
		TNT1 A 0 A_Jump(256, "Death1", "Death2")
	Death1:
		REVD ABC 3
		REVD D 3 A_Scream
		REVD E 3 A_NoBlocking
		REVD FGH 3
		REVD I -1
		Stop
	Death2:
		REVN ABC 3
		REVN D 3 A_Scream
		REVN E 3 A_NoBlocking
		REVN FG 3
		REVN H -1
		Stop
	XDeath:
		REDX A 3
		TNT1 A 0 A_JumpIf(GetCVAR("SD_Gore")==0, +2)
		TNT1 A 0 A_SpawnItemEx("GoreController")
		REDX B 3
		TNT1 A 0 A_XScream
		REDX C 2 A_Scream
		REDX D 2 A_NoBlocking
		REDX EFGHIJ 3
		REDX K -1
		Stop
	Death.Massacre:
		REDX AB 3
		TNT1 A 0 A_XScream
		REDX C 2 A_Scream
		REDX D 2 A_NoBlocking
		REDX EFGHIJ 3
		REDX K -1
		Stop
	Raise:
		TNT1 A 0 A_JumpIf(GetCVAR("SD_MoreDeaths")==0, "Raise1")
		TNT1 A 0 A_Jump(256, "Raise1", "Raise2")
	Raise1:
		REVD HGFEDCBA 3
		Goto See
	Raise2:
		REVN GFEDCBA 3
		Goto See
	}
}

ACTOR SDRevenantTracer : RevenantTracer replaces RevenantTracer
{
	decal revenantscorch
	States
	{
	Spawn:
		FATB ABCDCB 2 A_Tracer
		Loop
	Death:
		RBXP ABCDEFGHI 2 Bright
		Stop
	}
}